#+TITLE: My doom-emacs setup
#+AUTHOR: Vladimir Timofeenko
#+EMAIL: id@vtimofeenko.com
#+DATE: <2022-12-20 Tue>
#+PROPERTY: header-args:elisp :tangle doom.d/config.el :padline no
#+TAGS[]:
#+TAGS: fix(b) feat(f) doc(d) chore(c) to_think(t)
#+TAGS: noexport
#+HUGO_BASE_DIR: ~/Documents/vtimofeenko.com/
#+HUGO_SECTION: posts
#+HUGO_LEVEL_OFFSET: 0
#+HUGO_CODE_FENCE: nil
#+EXPORT_FILE_NAME: doom-emacs config

A literate programming Doom emacs setup centered around GTD and Nix development.

#+hugo: more

The full source of my config is [[https://github.com/VTimofeenko/doom-config][here]].


* Intro :noexport:
#+begin_src elisp
;;; $DOOMDIR/config.el --- Doom-emacs configuration file
;;; Commentary:
;;; Code:

(setq user-full-name "Vladimir Timofeenko"
      user-mail-address "id@vtimofeenko.com")
(setq doom-font (font-spec :family "JetBrainsMono Nerd Font" :size 12))
(setq doom-theme 'doom-one)
(setq display-line-numbers-type 'relative)
;; Configure the terminal cursor to change
(use-package! evil-terminal-cursor-changer
  :hook (tty-setup . evil-terminal-cursor-changer-activate))
;; Make the line numbers more visible
(custom-set-faces!
  '(line-number-current-line :foreground "#9A70A4")
  '(line-number :foreground "#A1A19A")
  )
(setq ispell-personal-dictionary "~/.cache/aspell.pws")
(defun zz/org-download-paste-clipboard (&optional use-default-filename)
  (interactive "P")
  (require 'org-download)
  (let ((file
         (if (not use-default-filename)
             (read-string (format "Filename [%s]: "
                                  org-download-screenshot-basename)
                          nil nil org-download-screenshot-basename)
           nil)))
    (org-download-clipboard file)))
(when (eq system-type 'darwin)
  (require 'ejc-sql)
  (setq nrepl-sync-request-timeout 60)
  (setq clomacs-httpd-default-port 8090) ; Use a port other than 8080.
  (after! org
    (add-to-list 'org-structure-template-alist
                 '("sql" . "src sql :exports both :eval no-export\n"))))
;; Add lines of context
(setq scroll-margin 5)
;; Automatically format .nix files on save
(add-hook 'nix-mode-hook 'nixpkgs-fmt-on-save-mode)
;; Add custom surround for nix multiline variables
(after! evil-surround
  (let ((pairs '((?m "''\n" . "\n''"))))
    (prependq! evil-surround-pairs-alist pairs)
    (prependq! evil-embrace-evil-surround-keys (mapcar #'car pairs))))
(setq datetime-timezone #'US/Pacific)
;; A very simple semantic commits implementation
;; Queries the user for the issue type and inserts it
(define-derived-mode vt-git-commit-mode text-mode "Git commit"
  (save-match-data
    (when (save-excursion (re-search-forward "\\`[\n[:space:]]*#" nil :noerror))
      (let (
            (committype (completing-read "Choose semantic commit type: "
                                         '("fix" "feat" "chore" "doc") nil t)))
        (save-excursion
          (insert (format "%s: \n" committype)))))))

(setq git-commit-major-mode 'vt-git-commit-mode)
#+end_src
* Orgmode config
** Intro :noexport:
#+begin_src elisp
(after! org
  (setq calendar-week-start-day 1)
  (setq org-log-done 'time)
  (setq org-log-into-drawer "LOGBOOK")
  ;; More intuitive link opening
  (map! :leader
        (
         :prefix-map ("l" . "link")
         :desc "Open link at cursor" "o" #'org-open-at-point
         )
        )

  (setq org-archive-location ".archive/%s_archive::")
  ;; Jump back-forth between visible headers
  (map! :leader
        (:desc "Next visible heading" "]" #'outline-next-visible-heading)
        )
  (map! :leader
        (:desc "Previous visible heading" "[" #'outline-previous-visible-heading)
        )
  (setq org-download-method 'directory)
  (setq org-download-image-dir "images")
  (setq org-download-heading-lvl nil)
  (setq org-download-timestamp "%Y%m%d-%H%M%S_")
  (setq org-image-actual-width 300)
  (map! :leader
        :prefix-map ("v" . "paste")
        (:desc "Paste image from clipboard" "i" #'zz/org-download-paste-clipboard))
  (add-to-list 'org-modules 'org-habit)
  (set 'org-habit-show-all-today t)
#+end_src
** General philosophy

This is mostly written to organize my own thoughts into some coherent narrative so that I stop stumbling over my own shoelaces with ad-hoc configuration.

This is done on top of a pretty vanilla doom-emacs configuration.

** GTD process implementation

*** *Capture* anything that crosses your mind, nothing is too big or small

#+DOWNLOADED: screenshot @ 2022-10-14 20:04:13
[[file:images/20221014-200413_capture.png]]

As someone who uses orgmode to organize both my professional and personal life, I have four categories of incoming information:

1. Tasks, containing some action to be done
2. Notes, generally not actionable, or for future reference
3. Meetings, for which notes should be taken
4. Appointments, which are set sometime in the future

The difference between the middle two is that for meetings I usually want to capture additional meta information, like when the meeting took place and who attended it.

The four kinds of incoming information should easily distinguishable. To that end, there are two mechanisms:

1. Keywords which denote the state of the task
2. Tags

I will be using a limited amount of TODO states, only tasks will have a keyword.

Meetings will be marked with a ":meeting:" tag.

Appointments will be marked with a ":appointment:"


Tasks, notes and appointments will have a property that captures when they were created.

#+begin_src elisp
(setq org-capture-templates
      `(("t" "Task" entry (file "inbox.org")
         ,(string-join '("* TODO %?"
                         ":PROPERTIES:"
                         ":CREATED: %U"
                         ":END:")
                       "\n"))
       ("n" "Note" entry (file "inbox.org")
         ,(string-join '("* %?"
                         ":PROPERTIES:"
                         ":CREATED: %U"
                         ":END:")
                       "\n"))
        ("m" "Meeting" entry (file "inbox.org")
         ,(string-join '("* %? :meeting:"
                         "<%<%Y-%m-%d %a %H:00>>"
                         ""
                         "/Met with: /")
                       "\n"))
        ("a" "Appointment" entry (file "inbox.org")
         ,(string-join '("* %? :appointment:"
                         ":PROPERTIES:"
                         ":CREATED: %U"
                         ":END:")
                       "\n"))
        ))
#+end_src

**** Task states (aka keywords)

In the spirit of simplicity, I will be using a limited set of task states:

- TODO :: to be done in future
- STRT :: being done right now
- HOLD :: cannot proceed, waits for some external blocker
- DONE :: terminal state, task is done
- CNCL :: terminal state, task canceled

#+begin_src elisp
(setq org-todo-keywords
      '((sequence "TODO(t)" "STRT(s)" "HOLD(h)" "|" "DONE(d)" "CNCL(c)")))
#+end_src

With their semantic meanings expressed by org-todo-keyword-faces:
#+begin_src elisp
(setq org-todo-keyword-faces '(("STRT" . +org-todo-active)
                               ("HOLD" . +org-todo-onhold)
                               ("CNCL" . +org-todo-cancel)))
#+end_src

*** *Clarify* what you've captured into clear and concrete action steps.

#+DOWNLOADED: screenshot @ 2022-10-14 20:04:51
[[file:images/20221014-200451_clarify.png]]

Clarification step involves two parts:

1. Locating objects to clarify
2. Editing an item for clarity, potentially breaking it into several items

The first point will be implemented by a custom entry in ~org-agenda-custom-commands~:

#+begin_src elisp
(setq org-agenda-custom-commands
      '(("g" "Get Things Done (GTD)"
         ;; Only show entries with the tag "inbox" -- just in case some entry outside inbox.org still has that file
         ((tags "inbox"
                ((org-agenda-prefix-format "  %?-12t% s")
                 ;; The list of items is already filtered by this tag, no point in showing that it exists
                 (org-agenda-hide-tags-regexp "inbox")
                 ;; The header of this section should be "Inbox: clarify and organize"
                 (org-agenda-overriding-header "\nInbox: clarify and organize\n")))))))
#+end_src

"Editing an item for clarity" is a bit more ambiguous term, since "clear" and "concrete" are hard to measure in technical terms.

However, there are a couple of technical steps that can be taken:

   1. Tasks can have an estimate, making it easier to pack into period when planning them. If I have an hour of free time - probably no point in tackling something that takes 2 hours to complete.

      A list of estimates that can used will be specified in ~inbox.org~ as:

      #+begin_src org
      #+PROPERTY: Effort_ALL 0 0:05 0:10 0:15 0:30 0:45 1:00 2:00 4:00
      #+end_src

      Since this value is specified in the inbox.org file and org-capture-templates are added there - it's possible to estimate effort directly when capturing a task.

   2. Make a captured task and note link back to the context they were taken in.

      I will be using ~add-to-list~ to shadow the previous values in ~org-capture-templates~. The new versions of a task and note will have an extra line linking to the context they were taken in (placeholder ~%a~)

      The downside of this approach is that "t" and "n" appear twice in capture list, but it's only cosmetic.

      #+begin_src elisp
(add-to-list 'org-capture-templates
             `("t" "Task" entry (file "inbox.org")
               ,(string-join '("* TODO %?"
                               ":PROPERTIES:"
                               ":CREATED: %U"
                               ":END:"
                               "/Context:/ %a")
                             "\n"
                             )))
(add-to-list 'org-capture-templates
             `("n" "Note" entry (file "inbox.org")
               ,(string-join '("* %?"
                               ":PROPERTIES:"
                               ":CREATED: %U"
                               ":END:"
                               "/Context:/ %a")
                             "\n")))
      #+end_src



**** DONE [#A] List tasks with huge(>1h) efforts as candidates for breaking down into smaller chunks :feat:
CLOSED: [2022-10-15 Sat 21:52]

**** TODO Create a t-shirt size mapping with Effort_ALL :feat:

I prefer XSmall, Small, Medium, Large, XLarge approach to effort estimates. In Youtrack I have created a mapping between the two. Would be convenient to have it in the orgmode.

**** TODO Consider "CREATED" prop for headings entered inline. :to_think:

[[https://stackoverflow.com/questions/12262220/add-created-date-property-to-todos-in-org-mode][Here's]] an implementation of this

**** TODO [#C] Fix extra entries in capture list :fix:

There are duplicates in the capture list because of add-to-list behavior. Not very important since only cosmetic.

*** *Organize* and put everything into the right place.

#+DOWNLOADED: screenshot @ 2022-10-14 20:06:08
[[file:images/20221014-200608_organize.png]]

1. The end-result of organization - empty inbox
2. Tasks get refiled into specific projects
3. Tasks that don't map to a project go to a special section of agenda
4. Meetings get refiled into journal inside agenda
5. Notes get refiled into specific project
6. Notes without a project go to notes.org

Since I could not make the setup below work, the refiling is just something I will have to do mentally.

**** TODO Context-dependent refile targets :feat:
Implementation of this approach would require context-dependent refile targets -- something that is not present in default emacs. However, it can be exnteded to support this (source: [[https://emacs.stackexchange.com/questions/24976/org-mode-can-you-set-up-context-dependent-refile-targets][StackOverflow]]):

#+begin_src elisp :tangle no
(require 'dash)

(defvar org-refile-contexts "Contexts for `org-capture'.

Takes the same values as `org-capture-templates-contexts' except
that the first value of each entry should be a valid setting for
`org-refile-targets'.")

(defun org-refile--get-context-targets ()
  "Get the refile targets for the current headline.

Returns the first set of targets in `org-refile-contexts' that
the current headline satisfies, or `org-refile-targets' if there
are no such."
  (or (car (-first (lambda (x)
                     (org-contextualize-validate-key
                      (car x)
                      org-refile-contexts))
                   org-refile-contexts
                   ))
      org-refile-targets)
  )

(defun org-refile-with-context (&optional arg default-buffer rfloc msg)
  "Refile the headline to a location based on `org-refile-targets'.

Changes the set of available refile targets based on `org-refile-contexts', but is otherwise identical to `org-refile'"
  (interactive "P")
  (let ((org-refile-targets (org-refile--get-context-targets)))
    (org-refile arg default-buffer rfloc msg)
    ))
#+end_src

Now, to implement the requirements at the beginning of this section:

#+begin_src elisp
(setq org-refile-contexts
      '((((("inbox.org") . (:regexp . "Projects"))) ;; example
         ((lambda () (string= (org-find-top-headline) "Inbox")))
         )
        ;; 6: Notes without a project go to notes.org
        (((("inbox.org") . (:regexp . "Notes")))
         ;;((lambda () (string= (org-element-property :my_type (org-element-at-point)) "NOTE")))
         ((lambda () ('regexp ":my_type:")))
         )
        ))
#+end_src

Journal-like results could be achieved through ~(file+olp+datetree)~

*** *Review*, update, and revise your lists.

#+DOWNLOADED: screenshot @ 2022-10-14 20:09:41
[[file:images/20221014-200941_review.png]]

The idea behind the implementation is to create a view to help move tasks along from triage, into refiled into started and ultimately done.

When showing the "Can be done" list, it's useful to have a quick reference to the day's agenda to see if I can actually fit something.

#+begin_src elisp
(setq org-agenda-files (list "inbox.org" "agenda.org"
                             "notes.org" "projects.org"))
(setq org-agenda-custom-commands
      '(("g" "Get Things Done (GTD)"
         ;; Only show entries with the tag "inbox" -- just in case some entry outside inbox.org still has that file
         ((tags "inbox"
                ((org-agenda-prefix-format "  %?-12t% s")
                 ;; The header of this section should be "Inbox: clarify and organize"
                 (org-agenda-overriding-header "\nInbox: clarify and organize\n")))
          ;; Show tasks that can be started and their estimates, do not show inbox
          (todo "TODO"
                ((org-agenda-skip-function
                  '(org-agenda-skip-entry-if 'deadline 'scheduled))
                 (org-agenda-files (list "agenda.org" "notes.org" "projects.org"))
                 (org-agenda-prefix-format "  %i %-12:c [%e] ")
                 (org-agenda-max-entries 5)
                 (org-agenda-overriding-header "\nTasks: Can be done\n")))
          ;; Show agenda around today
          (agenda nil
                  ((org-scheduled-past-days 0)
                   (org-deadline-warning-days 0)))
          ;; Show tasks on hold
          (todo "HOLD"
                ((org-agenda-prefix-format "  %i %-12:c [%e] ")
                 (org-agenda-overriding-header "\nTasks: on hold\n")))
          ;; Show tasks that are in progress
          (todo "STRT"
                ((org-agenda-prefix-format "  %i %-12:c [%e] ")
                 (org-agenda-overriding-header "\nTasks: in progress\n")))

          ;; Show tasks that I completed today
          (tags "CLOSED>=\"<today>\""
                ((org-agenda-overriding-header "\nCompleted today\n"))))
         (
          ;; The list of items is already filtered by this tag, no point in showing that it exists
          (org-agenda-hide-tags-regexp "inbox")))
        ("G" "All tasks that can be done"
         ((todo "TODO"
                ((org-agenda-skip-function
                  '(org-agenda-skip-entry-if 'deadline 'scheduled))
                 (org-agenda-files (list "agenda.org" "notes.org" "projects.org")) (org-agenda-prefix-format "  %i %-12:c [%e] ")
                 (org-agenda-overriding-header "\nTasks: Can be done\n")))
          (agenda nil
                  ((org-scheduled-past-days 0)
                   (org-deadline-warning-days 0)))))))
#+end_src

To show the agenda in a more compact manner and skip a time line when something is scheduled:

#+begin_src elisp
(setq org-agenda-time-grid
  '((daily today require-timed remove-match)
    (800 1000 1200 1400 1600 1800 2000)
    "......"
    "----------------"))
#+end_src

When something is scheduled for a specified time slot (08:00, 10:00, etc.), only the scheduled item will be shown, not the full "08:00 ... ..." line.

A view with tasks that are quick and slow would be useful:

1. Quick(<=15m) tasks can be neatly packed into focus times
2. Slow(>=2h) tasks are candidates for breaking down

#+begin_src elisp
;; taken from stackexchange
;; https://emacs.stackexchange.com/questions/59357/custom-agenda-view-based-on-effort-estimates
(defun fs/org-get-effort-estimate ()
  "Return effort estimate when point is at a given org headline.
If no effort estimate is specified, return nil."
  (let ((limits (org-get-property-block)))
    (save-excursion
      (when (and limits                            ; when non-nil
                 (re-search-forward ":Effort:[ ]*" ; has effort estimate
                                    (cdr limits)
                                    t))
        (buffer-substring-no-properties (point)
                                        (re-search-forward "[0-9:]*"
                                                           (cdr limits)))))))
(defun fs/org-search-for-quickpicks ()
  "Display entries that have effort estimates inferior to 15.
ARG is taken as a number."
  (let ((efforts (mapcar 'org-duration-from-minutes (number-sequence 1 15 1)))
        (next-entry (save-excursion (or (outline-next-heading) (point-max)))))
    (unless (member (fs/org-get-effort-estimate) efforts)
      next-entry)))
(defun vt/org-search-for-long-tasks ()
  "Display entries that have effort estimates longer than 1h "
  (let ((efforts (mapcar 'org-duration-from-minutes (number-sequence 120 600 1)))
        (next-entry (save-excursion (or (outline-next-heading) (point-max)))))
    (unless (member (fs/org-get-effort-estimate) efforts)
      next-entry)))

(add-to-list 'org-agenda-custom-commands
             '("E" "Efforts view"
               ((alltodo ""
                         ((org-agenda-skip-function 'fs/org-search-for-quickpicks)
                          (org-agenda-overriding-header "Quick tasks")))
                (alltodo ""
                         ((org-agenda-skip-function 'vt/org-search-for-long-tasks)
                          ;; For longer tasks - show how long they are
                          (org-agenda-prefix-format "[%e] ")
                          (org-agenda-overriding-header "Long tasks"))))))
#+end_src

**** DONE Remove scheduled tasks from 'can be done' :fix:
CLOSED: [2022-10-07 Fri 12:26]

**** DONE [#A] Sort 'can be done' tasks by priority :feat:
CLOSED: [2022-10-11 Tue 11:48]

The current default sorting is sufficient

**** TODO [#C] Rewrite this using add-to-list to maintain coherency in tangled file :chore:

**** TODO [#C] Add periodic tasks that were complete today to the list of complete tasks :feat:

**** CNCL Show task with duration as a continuous block :feat:

Maybe like [[https://emacs.stackexchange.com/questions/5395/show-free-blocks-of-time-in-org-modes-agenda][so]]?

Not sure how it's implementable now, but with the ~org-agenda-time-grid~ config is slightly better

**** TODO Do not show tasks under a project that is on HOLD :feat:

Applies to both "g" and "G" views.

If I put a project on pause - I don't need to see its tasks

**** CNCL Add a view only for projects and their states :feat:

Just include top-level headlines from the ~projects.org~ and their states. Maybe percentage of tasks if I can figure out how to do it.

Since in my setup the projects are just headlines - best way to view this is to ~<S-TAB>~ in ~projects.org~.

**** TODO Add effort/clocks to "Completed today" :feat:

**** TODO Show recurring tasks that were completed today :feat:

**** TODO Show tasks without a set time at the top of the list :feat:

*** *Engage* Get to work on the important stuff.

Well that's the easiest part. Just go and do stuff.

**** TODO Consider having separate views for the proces

A "review" view could be implemented like [[https://stackoverflow.com/questions/22394394/orgmode-a-report-of-tasks-that-are-done-within-the-week][so]]
**** TODO Make sure the projects priorities propagate to the tasks :feat:

** Journaling process

*** TODO Add a way to link the day note to the agenda :feat:

Maybe a separate capture template like journal?

** Habits

** File organization

My ~~/org/~ directory should be as clean as possible with only the following files present:

- ~inbox.org~ :: file for incoming notes
- ~agenda.org~ :: file for

** Literate programming

While developing in literate programming style, I am using a certain set of languages -- these structures help quickly add common code blocks to the documents:

#+begin_src elisp
(add-to-list 'org-structure-template-alist
             '("elisp" . "src elisp\n"))
(add-to-list 'org-structure-template-alist
             '("lua" . "src lua\n"))
(add-to-list 'org-structure-template-alist
             '("nix" . "src nix\n"))
#+end_src

*** TODO add the auto-tangle templates and useful tags here

** Helpers
*** Org-capture outside emacs

Since apparently I still have operating system outside of Emacs - it's useful to be able to capture something in org-mode without having to switch to emacs. The quick capture function from this [[https://abelstern.nl/posts/emacs-quick-capture/][source]] helps by providing a wrapper around org-capture that appears in a popup window, captures what's needed and then disappears.

#+begin_src elisp
(defun abs--quick-capture ()
  ;; redefine the function that splits the frame upon org-capture
  (defun abs--org-capture-place-template-dont-delete-windows (oldfun args)
    (cl-letf (((symbol-function 'org-switch-to-buffer-other-window) 'switch-to-buffer))
      (apply oldfun args)))

  ;; run-once hook to close window after capture
  (defun abs--delete-frame-after-capture ()
    (delete-frame)
    (remove-hook 'org-capture-after-finalize-hook 'abs--delete-frame-after-capture)
    )

  ;; set frame title
  (set-frame-name "emacs org capture")
  (add-hook 'org-capture-after-finalize-hook 'abs--delete-frame-after-capture)
  (abs--org-capture-place-template-dont-delete-windows 'org-capture nil))
#+end_src

With this bit of code, OS can trigger something like:

#+begin_src shell :tangle no
setsid -f emacsclient -q -c -e '(abs--quick-capture)' >/dev/null 2>&1
#+end_src

To bring up the capture window.

*** Automatically change TODO state based on checkbox

When working on a composite TODO, it saves a few keystrokes when the TODO state and the completion cookie get updated automatically. Taking the code from [[https://christiantietze.de/posts/2021/02/emacs-org-todo-doing-done-checkbox-cycling/][this source]]:

#+begin_src elisp
;; Changes the TODO state based on statistics cookie
(defun org-todo-if-needed (state)
  "Change header state to STATE unless the current item is in STATE already."
  (unless (or
           (string-equal (org-get-todo-state) state)
           (string-equal (org-get-todo-state) nil)) ;; do not change item if it's not in a state
    (org-todo state)))

(defun ct/org-summary-todo-cookie (n-done n-not-done)
  "Switch header state to DONE when all subentries are DONE, to TODO when none are DONE, and to DOING otherwise"
  (let (org-log-done org-log-states)   ; turn off logging
    (org-todo-if-needed (cond ((= n-done 0)
                               "TODO")
                              ((= n-not-done 0)
                               "DONE")
                              (t
                               "STRT")))))
(add-hook 'org-after-todo-statistics-hook #'ct/org-summary-todo-cookie)

(defun ct/org-summary-checkbox-cookie ()
  "Switch header state to DONE when all checkboxes are ticked, to TODO when none are ticked, and to DOING otherwise"
  (let (beg end)
    (unless (not (org-get-todo-state))
      (save-excursion
        (org-back-to-heading t)
        (setq beg (point))
        (end-of-line)
        (setq end (point))
        (goto-char beg)
        ;; Regex group 1: %-based cookie
        ;; Regex group 2 and 3: x/y cookie
        (if (re-search-forward "\\[\\([0-9]*%\\)\\]\\|\\[\\([0-9]*\\)/\\([0-9]*\\)\\]"
                               end t)
            (if (match-end 1)
                ;; [xx%] cookie support
                (cond ((equal (match-string 1) "100%")
                       (org-todo-if-needed "DONE"))
                      ((equal (match-string 1) "0%")
                       (org-todo-if-needed "TODO"))
                      (t
                       (org-todo-if-needed "STRT")))
              ;; [x/y] cookie support
              (if (> (match-end 2) (match-beginning 2)) ; = if not empty
                  (cond ((equal (match-string 2) (match-string 3))
                         (org-todo-if-needed "DONE"))
                        ((or (equal (string-trim (match-string 2)) "")
                             (equal (match-string 2) "0"))
                         (org-todo-if-needed "TODO"))
                        (t
                         (org-todo-if-needed "STRT")))
                (org-todo-if-needed "DOING"))))))))
(add-hook 'org-checkbox-statistics-hook #'ct/org-summary-checkbox-cookie)
;; Reset the child checkboxes when a todo task is repeated
(add-hook 'org-todo-repeat-hook #'org-reset-checkbox-state-subtree)
#+end_src

**** DONE Add a property to not trigger this automatically for counter-only headlines :feat:
CLOSED: [2023-01-02 Mon 11:17]

A better solution is to check if the state is nil and then don't do anything

*** A custom home_maintenance view that is published locally

I maintain a list of chores in a set of headlines tagged "home_maintenance". To share that list - it can be published on an internal webserver (last argument for ~org-agenda-custom-commands~).

The custom command retrieves all headlines with tags "home_maintenance"(~[1]~), but does not display that actual tag (~[2]~).

The state of the keyword (TODO/STRT/whatever) does not matter; as long as the task is not complete -- it will show up in the list (~[3]~);

#+begin_src elisp
(add-to-list 'org-agenda-custom-commands
             '("h" "home maintenance"
               ((agenda ""
                        ((org-agenda-span 7)
                         (org-agenda-start-on-weekday 1)
                         (org-agenda-time-grid nil)
                         (org-agenda-start-day "+0d") ;; Without this line the custom view seems to be stuck on the previous week
                         (org-agenda-repeating-timestamp-show-all t)
                         (org-agenda-prefix-format "%-12c:   ")
                         (org-agenda-hide-tags-regexp "home_maintenance") ;; [2]
                         (org-agenda-sorting-strategy '((agenda priority-down category-up time-up)
                                                        (todo priority-down category-keep)
                                                        (tags priority-down category-keep)
                                                        (search category-keep)))

                         (org-agenda-todo-keyword-format "") ;; [3]
                         (org-agenda-tag-filter-preset '("+home_maintenance")) ;; [1]
                         )))
               nil
               ("~/code/infra/services/dashy/home_maint.html")))
#+end_src

Now, whenever I need to publish this config, I can just run:

#+begin_src bash :tangle no
emacsclient -eval '(org-batch-store-agenda-views)'
#+end_src

which will save that custom view to the specified directory.

** References

- [[https://www.labri.fr/perso/nrougier/GTD/index.html][GET THINGS DONE WITH EMACS]] by NICOLAS P. ROUGIER

*** TODO Add other references :chore:

** Outro :noexport:
#+begin_src elisp

#+end_src
#+begin_src elisp
)
#+end_src
* General stuff
** TODO ispell setup works janky across machines :fix:
** TODO Language tool for spell/ortho checking :feat:

Howto [[https://github.com/mhayashi1120/Emacs-langtool][here]]
** DONE Move the file to the root of the project :chore:
CLOSED: [2022-10-07 Fri 19:15]
** DONE Add Excalidraw drawings to the sections :chore:
CLOSED: [2022-10-14 Fri 20:11]
** TODO Add better faces for priorities :feat:
** DONE Add tangle on save hook :feat:
CLOSED: [2022-10-14 Fri 20:01]
** TODO Add tangle on pre-commit hook :feat:
** TODO Integrate with TTRSS :feat:
** TODO Add auto-tangle back and forth :feat:
** TODO Record this file's skeleton as default literate project file :feat:
** TODO Add org capture from terminal :feat:
** TODO Add notification mechanisms :feat:
** TODO Disable spell check in local variables :chore:

* Project chores :noexport:
** TODO Rework the flake to minimize rebuilds :chore:
** TODO Add sections for the flake and rest of config :chore:
** TODO Add template for Nix project file :feat:
** TODO Add development shell with pngquant :chore:

* Project references
- [[https://zzamboni.org/post/my-doom-emacs-configuration-with-commentary/][Zzamboni literate config]]
- [[https://github.com/Mic92/dotfiles/blob/master/nixpkgs-config/modules/emacs/default.nix][Mic92 dotfiles]], editor service reference. Reference for no-rebuild doom config.

* Literate setup :noexport:

A few scripts to be automatically run:

- When the file is saved - tangle all code blocks
- On save export the file to a hugo markdown
- On save optimize all pngs using pngquant

;; Local Variables:
;; eval: (add-hook 'after-save-hook (lambda ()(org-babel-tangle)) nil t)
;; eval: (add-hook 'after-save-hook 'org-hugo-export-to-md t t)
;; eval: (add-hook 'after-save-hook (lambda()(shell-command "nix run nixpkgs#pngquant -- --ext .png --force ~/Documents/vtimofeenko.com/static/ox-hugo/*.png")))
;; End:
